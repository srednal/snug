// preamble cruft
import org.gradle.api.internal.file.FileResolver
import org.gradle.process.internal.DefaultJavaExecAction
import org.gradle.process.internal.JavaExecAction

import java.math.MathContext

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public" }
    }
    dependencies {
        classpath group: 'org.scoverage', name: 'gradle-scoverage', version: '0.5-SNAPSHOT'
    }
}
buildscript.repositories.each { project.repositories.add(it) }

project.group = project.properties.projectGroup
project.version = project.properties.projectVersion

/////////////////////////////////////////////////////////////////////
// The main useful bits:

apply plugin: 'scala'
apply plugin: 'scoverage'

defaultTasks 'clean', 'checkScoverage'

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.11.4'
    compile group: 'org.scala-lang', name: 'scala-reflect', version: '2.11.4'
    compile group: 'org.scala-lang', name: 'scalap', version: '2.11.4'

    compile group: 'com.typesafe.akka', name: 'akka-actor_2.11', version: '2.3.6'
    compile group: 'com.typesafe', name: 'config', version: '1.2.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'


    // test
    testCompile group: 'org.scalatest', name: "scalatest_2.11", version: '2.2.2'
    testCompile group: 'com.typesafe.akka', name: 'akka-testkit_2.11', version: '2.3.6'
    // pimpFuture
    testCompile group: 'io.spray', name: 'spray-util_2.11', version: '1.3.2'

    testCompile group: 'org.scalamock', name: 'scalamock-scalatest-support_2.11', version: '3.2'
    testRuntime group: 'org.pegdown', name: 'pegdown', version: '1.4.2'
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'

    scoverage group: 'org.scoverage', name: 'scalac-scoverage-plugin_2.11', version: '0.99.9'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false  // Zinc
}

checkScoverage {
    // actually, it's statement coverage now with scoverage but the property is still named line rate
    minimumLineRate = 0.85
}

////////////////////////////////////////////////////////////////
// Test cruft

task('scalatest', type: Test)
test.dependsOn scalatest

setupTest(scalatest)
setupTest(testScoverage)

scoverage {
    // There is a bug in something (gradle? scoverage? scoverage plugin?) where this can not be empty
    excludedPackages = 'scoverAllOfTheThings'
}

compileScoverageScala.scalaCompileOptions.useAnt = true // cast GString to String error w/ scoverage running zinc

def setupTest(Test t) {
    t.systemProperties['java.awt.headless'] = 'true'
    t.maxParallelForks = Runtime.runtime.availableProcessors()

    t << {
        // Based on com.github.maiflai:gradle-scalatest:0.5 but copied to gain control over scalatest options
        FileResolver fileResolver = t.getServices().get(FileResolver.class)

        JavaExecAction javaExecHandleBuilder = new DefaultJavaExecAction(fileResolver)
        javaExecHandleBuilder.setMain('org.scalatest.tools.Runner')
        javaExecHandleBuilder.setJvmArgs(t.allJvmArgs)
        javaExecHandleBuilder.setClasspath(t.classpath)

        List<String> args = new ArrayList<String>()
        args.add('-oSTD')
        args.add("-PS${maxParallelForks}".toString())
        args.addAll(['-R', t.testClassesDir.absolutePath])

        if (t.reports.junitXml.enabled) args.addAll(['-u', t.reports.junitXml.entryPoint.absolutePath])

        if (t.reports.html.enabled) {
            def dest = t.reports.html.destination
            dest.mkdirs()
            args.addAll(['-h', dest.absolutePath])
        }

        // Run a single test with -Dtest=com.srednal.snug.FooTest
        if (System.properties['test']) args.addAll(['-s', System.getProperty('test')])

        javaExecHandleBuilder.setArgs(args)
        javaExecHandleBuilder.execute()
    }
}

gradle.taskGraph.afterTask { Task task, TaskState state ->
    if (task == scalatest || task == testScoverage) {
        def dir = task.reports.html.destination.path - "$project.projectDir.path/"
        out.println("Test report: open $dir/index.html")
    }
    if (task == testScoverage) {
        // make the path relative
        def dir = scoverage.reportDir.path - "$project.projectDir.path/"
        println("Coverage report: open $dir/index.html")
    }
    if (task == checkScoverage) {
        def xml = checkScoverage.parser.parse(checkScoverage.cobertura)
        def coveragePct = new BigDecimal(xml.attribute('line-rate')).multiply(100).round(new MathContext(3))
        println("Code coverage: $coveragePct%")
    }
}

//////////////////////////////////////////////////////////

wrapper {
    gradleVersion = '2.2.1'
}
